package Analisis.Graphik;

import java_cup.runtime.Symbol;
import Extras.*;
import Analisis.*;
import Reportes.*;

parser code
{://Codigo visible

    public static Nodo raiz=new Nodo("raiz");
    public void syntax_error(Symbol s){
        TablaErrores.insertarError("Error Sintactico: "+s.value.toString()+" ",s.right,s.left);
        System.out.println(s.value.toString()+" Fila: " +s.right+", Columna: "+s.left);        
    }

    public void unrecovered_syntax_error(Symbol s){        
        TablaErrores.insertarError("Error Sintactico: "+s.value.toString()+" ",s.right,s.left);
        System.out.println(s.value.toString()+" Fila: " +s.right+", Columna: "+s.left);        
    }

:}

action code
{://Codigo de acciones
      
:}
    
terminal String var, tentero, tcadena, tdecimal, tcaracter, tvacio, tbool, importar, gk, als, hereda, nuevo, retornar, llamar, inicio, incluirhk;                
terminal String seleccion, caso, defecto, para, mientras, hacer, continuar, terminar, graphikar, datos, columna, procesar, dondecada;               
terminal String imprimir, pregc, dospuntos, igual, llavea, llavec, masmas, menosmenos, suma, resta, mult, div, potencia, parentesisa, parentesisc;
terminal String mayor, menor, igualacion, distinto, o, y, xor, not, punto, corchetea, corchetec,  decimal, num, cadena, id,caracter;
terminal String mayorigual, menorigual, dondetodo, donde, llamarhk, si, sino, publico, protegido, privado,coma,verdadero, falso;

nonterminal Nodo S,LENCABEZADO, ENC, LALS, ASIGNACION, IMPORTAR, HEREDA, CUERPOALS,ALS,SENTENCIA,LEXP;
nonterminal Nodo DECLARACION, ASIG, VALASIG, ARR,  ASIGARR, EXP, LID, DECFUN, DEC2,RETORNAR, SENTALS, POSTERIOR;
nonterminal Nodo PDECFUN, LLAMADO, LPFUN, INICIO, INCLUIR, IDL, SI, SINO, CUERPO, SELECCION, LCASO, CASO,DEFECTO,GRAPHIKAR;
nonterminal Nodo PARA, VARPARA, MIENTRAS,HACERMIENTRAS, DIM, LLLAVE,LLAVE,IMPRIMIR,ENCABEZADO,EXP2,EXP3,ID;
nonterminal String OPREL,OPPARA,TVAR,TFUN;
nonterminal Integer VISIBILIDAD,TVISIBILIDAD;

precedence left o;
precedence left xor;
precedence left y;
precedence right not;

precedence left mayor, menor, mayorigual,menorigual, distinto, igualacion;
precedence left suma, resta;
precedence left mult, div; 
precedence right potencia;
precedence right masmas, menosmenos;

start with S;

S ::=   ENC:enc LALS:lals
    {:
        System.out.println("Archivo finalizado");
        raiz=Fabrica.crearNodoGraphik(enc,lals);        
    :};

TVISIBILIDAD::= privado:privado{:RESULT = Constante.privado;:}
                             | publico:publico{:RESULT = Constante.publico;:}
                             | protegido:protegido{:RESULT = Constante.protegido;:};
VISIBILIDAD ::=  dospuntos TVISIBILIDAD:tvisibilidad{:RESULT = tvisibilidad;:}
                              |{:RESULT = Constante.publico;:};
ASIGNACION ::= LID:lid igual EXP:exp{:RESULT = Fabrica.crearNodoAsignacion(lid,exp,null);:}
                               | LID:lid ARR:arr igual EXP:exp{:RESULT = Fabrica.crearNodoAsignacion(lid,exp,arr);:}
                               | LID:lid igual nuevo id:id parentesisa parentesisc
                                {:
                                    Nodo nuevo= Fabrica.crearNodoNuevo(id);
                                    RESULT = Fabrica.crearNodoAsignacion(lid,nuevo,null);
                                :};
ENC ::= LENCABEZADO:encabezado{:RESULT = encabezado;:}
            |{:RESULT=Fabrica.crearNodoEnc(null);:};
LENCABEZADO ::= LENCABEZADO:encabezado ENCABEZADO:enc{:RESULT = Fabrica.insertarEnc(encabezado,enc);:}
                            |ENCABEZADO:enc{:RESULT = Fabrica.crearNodoEnc(enc);:};
ENCABEZADO ::= IMPORTAR:importar{:RESULT = importar;:}
                                | INCLUIR:incluir {:RESULT = incluir;:};
IMPORTAR ::= importar id:id gk pregc
                         {:
                            Nodo id_imp = Fabrica.crearNodoHoja(Constante.id,id);
                            RESULT = Fabrica.crearNodoImportar(id_imp);
                         :};
LALS ::= LALS:lals ALS:als{:RESULT = Fabrica.insertarALS(lals,als);:}
             | ALS:als{:RESULT = Fabrica.crearNodoLALS(als);:};
ALS ::= als id:id HEREDA:hereda VISIBILIDAD:visibilidad llavea CUERPOALS:cuerpo llavec{:RESULT = Fabrica.crearNodoALS(id,hereda,visibilidad,cuerpo);:};
CUERPOALS ::= CUERPOALS:cuerpo SENTALS:sent{:RESULT = Fabrica.insertarSentencia(cuerpo,sent);:}
                         | SENTALS:sent{:RESULT = Fabrica.crearNodoCuerpo(sent);:};
SENTALS ::= DECFUN:decfun{:RESULT = decfun;:}
                        | DECLARACION:dec  pregc{:RESULT = dec;:}
                        | INICIO:inicio{:RESULT = inicio;:};
HEREDA ::= hereda id:id{:RESULT = Fabrica.crearNodoHereda(id);:}
                     |{:RESULT = Fabrica.crearNodoHereda("");:};
DECLARACION ::= var TVAR:tvar id:id VISIBILIDAD:visibilidad DEC2:dec2{:RESULT = Fabrica.crearNodoDeclaracion(tvar,id,visibilidad,dec2);:};
DEC2 ::= IDL:idl{:RESULT=idl;:}
                | ASIG:asig {:RESULT = asig;:}
                |{:RESULT = null;:};
IDL ::= IDL:idl coma id:id VISIBILIDAD:visibilidad
                {:
                Nodo id1=Fabrica.crearNodoHoja(Constante.id,id); 
                id1.setVisibilidad(visibilidad);
                RESULT = Fabrica.insertarIDL(idl,id1);
                :}
            | coma id:id VISIBILIDAD:visibilidad
                {:
                Nodo id1=Fabrica.crearNodoHoja(Constante.id,id);
                id1.setVisibilidad(visibilidad);
                RESULT = Fabrica.crearNodoIDL(id1);
                :};
ASIG ::= igual VALASIG:valasig{:RESULT = valasig;:}
              | ARR:arr ASIGARR:asig{:RESULT = Fabrica.crearNodoArr(arr,asig);:}; 
ARR ::= ARR:arr corchetea EXP:exp corchetec{:RESULT = Fabrica.insertarCorchete(arr,exp);:}                
                |corchetea EXP:exp corchetec{:RESULT=Fabrica.crearNodoL_Corchete(exp);:};

ASIGARR ::= igual LLAVE:llave{:RESULT = llave;:}
                      | {:RESULT = Fabrica.crearNodoL_Llave(null);:};
LLAVE ::= llavea LLLAVE:lllave llavec{:RESULT = lllave;:}
                     | llavea LPFUN:lpfun llavec{:RESULT = lpfun;:};                     
LLLAVE ::= LLLAVE:lllave coma LLAVE:llave{:RESULT = RESULT=Fabrica.insertarLlave(lllave,llave);:}
                    | LLAVE:llave{:RESULT = Fabrica.crearNodoL_Llave(llave);:};
VALASIG ::= EXP:exp{:RESULT = exp;:}
                       | nuevo id:id parentesisa parentesisc {:RESULT = Fabrica.crearNodoNuevo(id);:};
LID ::= LID:lid punto ID:id
                {:                                        
                    RESULT = Fabrica.insertarLID(lid,id);
                 :}             
                |  ID:id       {:RESULT = Fabrica.crearNodoLID(id);:};
ID ::= id:id  {: RESULT=Fabrica.crearNodoHoja(Constante.id,id);:}                
            |id:id parentesisa LPFUN:pdecfun parentesisc
            {:
                RESULT=Fabrica.crearNodoHoja(Constante.id,id);
                RESULT.insertarHijo(pdecfun);
            :};
DECFUN ::= TFUN:tfun id:id parentesisa PDECFUN:pdecfun parentesisc VISIBILIDAD:visibilidad llavea CUERPO:cuerpo llavec
                    {:RESULT = Fabrica.crearNodoDecFun(tfun,id,pdecfun,visibilidad,cuerpo);:};
TVAR ::= tentero{:RESULT = Constante.tnum+"";:}
                | tdecimal  {:RESULT = Constante.tdecimal+"";:}
                | tcaracter{:RESULT = Constante.tcaracter+"";:}
                | tcadena{:RESULT = Constante.tcadena+"";:}
                | tbool{:RESULT = Constante.tbool+"";:}
                | id:id{:RESULT = id;:};
TFUN ::= TVAR:tvar{:RESULT = tvar;:}
               | tvacio{:RESULT = Constante.tvacio+"";:};
PDECFUN ::= PDECFUN:pdecfun coma TVAR:tvar id:id
                        {:
                        Nodo par=Fabrica.crearNodoHoja(Constante.id,id,tvar);
                        RESULT= Fabrica.insertarPar(pdecfun,par);
                        :}
                        | TVAR:tvar id:id
                        {:
                        Nodo par=Fabrica.crearNodoHoja(Constante.id,id,tvar);
                        RESULT= Fabrica.crearNodoLPAR(par);
                        :}
                        |{:RESULT= Fabrica.crearNodoLPAR(null);:};
RETORNAR ::= retornar EXP:exp{:RESULT = new Nodo(Constante.retornar); RESULT.insertarHijo(exp);:};
LLAMADO ::= llamar LID:lid {:RESULT = Fabrica.crearNodoLlamado(lid);:}
                         | llamarhk id:id parentesisa LPFUN:lpfun parentesisc{:RESULT = Fabrica.crearNodoLlamado(id,lpfun);:};
LPFUN ::= LPFUN:lpar coma EXP:exp{:RESULT=Fabrica.insertarPar(lpar,exp);:}
                 | EXP:exp{:RESULT=Fabrica.crearNodoLPAR(exp);:}
                 |{:RESULT=Fabrica.crearNodoLPAR(null);:};
INICIO ::= tvacio inicio parentesisa parentesisc llavea CUERPO:cuerpo llavec{:RESULT = Fabrica.crearNodoInicio(cuerpo);:};
INCLUIR ::= incluirhk id:id pregc
                    {:
                        Nodo id_inc = Fabrica.crearNodoHoja(Constante.id, id);
                        RESULT = Fabrica.crearNodoIncluir(id_inc);
                    :};
SI ::= si parentesisa EXP:exp parentesisc llavea CUERPO:cuerpo llavec SINO:sino{:RESULT = Fabrica.crearNodoSi(exp,cuerpo,sino);:};
SINO ::= sino llavea CUERPO:cuerpo llavec{:RESULT = cuerpo;:}
             | {:RESULT = null;:};
SELECCION ::= seleccion parentesisa LID:lid parentesisc llavea LCASO:lcaso DEFECTO:defecto llavec
                {:                    
                    if(defecto!=null){
                        lcaso.insertarHijo(defecto);
                    }
                    RESULT = Fabrica.crearNodoSentCaso(lid,lcaso);
                :};
LCASO ::= LCASO:lcasos CASO:caso{:RESULT = Fabrica.insertarCaso(lcasos,caso);:}
                    | CASO:caso{:RESULT = Fabrica.crearNodoLCasos(caso);:};
CASO ::= caso EXP:exp dospuntos CUERPO:cuerpo{:RESULT = Fabrica.crearNodoCaso(exp,cuerpo);:};
DEFECTO ::= defecto dospuntos CUERPO:cuerpo{:RESULT = new Nodo(Constante.defecto); RESULT.insertarHijo(cuerpo);:}
                        |{:RESULT = null;:};
PARA ::= para parentesisa VARPARA:varpara dospuntos EXP:exp dospuntos POSTERIOR:pos parentesisc llavea CUERPO:cuerpo llavec
                {:                
                RESULT = Fabrica.crearNodoPara(varpara,exp,pos,cuerpo);
                :};

VARPARA::= var TVAR:tvar id:id igual EXP:exp{:RESULT = Fabrica.crearNodoDeclaracion(tvar,id,-1,exp);:}  
                     | LID:lid igual EXP:exp{:RESULT = Fabrica.crearNodoAsignacion(lid,exp,null);:};
POSTERIOR ::= LID:lid OPPARA:op
                            {:
                                Nodo op2=Fabrica.crearNodoHoja(Constante.num,"1",Constante.tnum+"");
                                Nodo exp = Fabrica.crearNodoOperacion(op,lid,op2);
                                RESULT = Fabrica.crearNodoAsignacion(lid,exp,null);
                            :}
                           |ASIGNACION:asignacion{:RESULT = asignacion;:};
OPPARA ::= masmas{:RESULT = Constante.suma;:}
                     | menosmenos{:RESULT = Constante.resta;:};
MIENTRAS ::= mientras parentesisa EXP:exp parentesisc llavea CUERPO:cuerpo llavec{:RESULT = Fabrica.crearNodoMientras(Constante.mientras,exp,cuerpo);:};
HACERMIENTRAS ::= hacer llavea CUERPO:cuerpo llavec mientras parentesisa EXP:exp parentesisc{:RESULT = Fabrica.crearNodoMientras(Constante.hacermientras,exp,cuerpo);:};

EXP ::= EXP:op1 o EXP:op2 {:RESULT=Fabrica.crearNodoOperacion(Constante.o,op1,op2);:}   
             | EXP:op1 y EXP:op2 {:RESULT=Fabrica.crearNodoOperacion(Constante.y,op1,op2);:}   
             | EXP:op1 xor EXP:op2 {:RESULT=Fabrica.crearNodoOperacion(Constante.xor,op1,op2);:}                
             | not EXP:op1 {:RESULT=Fabrica.crearNodoOperacion(Constante.not,op1);:}
             | EXP2:exp2{:RESULT = exp2;:};            
EXP2::= EXP3:op1 OPREL:oprel EXP3:op2 {:RESULT=Fabrica.crearNodoOperacion(oprel,op1,op2);:}   
                | EXP3:exp3{:RESULT = exp3;:};
EXP3::=EXP3:op1 suma EXP3:op2{:RESULT=Fabrica.crearNodoOperacion(Constante.suma,op1,op2);:}
             | EXP3:op1 resta EXP3:op2 {:RESULT=Fabrica.crearNodoOperacion(Constante.resta,op1,op2);:}
             | EXP3:op1 mult EXP3:op2 {:RESULT=Fabrica.crearNodoOperacion(Constante.mult,op1,op2);:}             
             | EXP3:op1 div EXP3:op2 {:RESULT=Fabrica.crearNodoOperacion(Constante.div,op1,op2);:}                          
             | EXP3:op1 potencia EXP3:op2 {:RESULT=Fabrica.crearNodoOperacion(Constante.pot,op1,op2);:}             
             | resta EXP3:op1 {:RESULT=Fabrica.crearNodoOperacion(Constante.resta,op1);:}             
             | parentesisa EXP:exp parentesisc {:RESULT=exp;:}            
             | LLAMADO:llamado{:RESULT = llamado;:}
             | num:num {:RESULT = Fabrica.crearNodoHoja(Constante.num,num,Constante.tnum+"");:}
             | cadena:cadena {:RESULT = Fabrica.crearNodoHoja(Constante.cadena,cadena,Constante.tcadena+"");:}
             | caracter:caracter {:RESULT = Fabrica.crearNodoHoja(Constante.caracter,caracter,Constante.tcaracter+"");:}
             | LID:lid{:RESULT = lid;:}
             | LID:lid ARR:arr{:RESULT =lid;RESULT.insertarHijo(arr);:}
             | decimal:dec{:RESULT = Fabrica.crearNodoHoja(Constante.decimal,dec,Constante.tdecimal+"");:}
             | verdadero:verdadero {:RESULT = Fabrica.crearNodoHoja(Constante.bool,Constante.verdadero,Constante.tbool+"");:}
             | falso:falso {:RESULT = Fabrica.crearNodoHoja(Constante.bool,Constante.falso,Constante.tbool+"");:};
OPREL ::= mayor {:RESULT = Constante.mayor;:}
                    | menor{:RESULT = Constante.menor;:}
                    | mayorigual{:RESULT = Constante.mayorigual;:}
                    | menorigual{:RESULT = Constante.menorigual;:}
                    | igualacion{:RESULT = Constante.igualacion;:}
                    | distinto{:RESULT = Constante.distinto;:};

CUERPO ::= CUERPO:cuerpo SENTENCIA:sent {:RESULT = Fabrica.insertarSentencia(cuerpo,sent);:}
                     | SENTENCIA:sent {:RESULT=Fabrica.crearNodoCuerpo(sent);:};

SENTENCIA ::= ASIGNACION:asignacion pregc {:RESULT = asignacion;:}
                          | DECLARACION:declaracion pregc{:RESULT = declaracion;:}
                          | RETORNAR:retornar pregc{:RESULT = retornar;:}
                          | LLAMADO:llamado pregc{:RESULT = llamado;:}
                          | SI:si {:RESULT = si;:}
                          | SELECCION:seleccion{:RESULT = seleccion;:}
                          | PARA:para{:RESULT = para;:}
                          | MIENTRAS:mientras {:RESULT = mientras;:}
                          | HACERMIENTRAS:hacermientras{:RESULT = hacermientras;:}
                          | IMPRIMIR:imprimir{:RESULT = imprimir;:}
                          | GRAPHIKAR:graphikar {:RESULT = graphikar;:}
                          | continuar pregc{:RESULT = new Nodo(Constante.continuar);:}
                          | terminar pregc{:RESULT = new Nodo(Constante.terminar);:}
                          |LID:lid OPPARA:op pregc
                            {:
                                Nodo op2=Fabrica.crearNodoHoja(Constante.num,"1",Constante.tnum+"");
                                Nodo exp = Fabrica.crearNodoOperacion(op,lid,op2);
                                RESULT = Fabrica.crearNodoAsignacion(lid,exp,null);
                            :};

IMPRIMIR ::= imprimir parentesisa EXP:exp parentesisc pregc{:RESULT = new Nodo(Constante.imprimir); RESULT.insertarHijo(exp);:};

GRAPHIKAR ::= graphikar parentesisa LID:lid1 coma LID:lid2 parentesisc pregc
                            {:
                                RESULT = new Nodo(Constante.graphikar);
                                RESULT.insertarHijo(lid1);
                                RESULT.insertarHijo(lid2);
                            :};
